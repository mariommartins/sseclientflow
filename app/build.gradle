plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

def sensitiveProperty(name) {
    return getProperty('sensitive.properties', name)
}

private def getProperty(fileName, name) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file(fileName).newDataInputStream())
    return "\"${properties.getProperty(name)}\""
}

android {
    namespace 'com.sseclientflow'
    compileSdk build_versions.target_sdk

    defaultConfig {
        applicationId "com.sseclientflow"
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk
        versionCode build_versions.version_code
        versionName build_versions.version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'SSE_BASE_URL', sensitiveProperty('SSE_BASE_URL')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility build_versions.java
        targetCompatibility build_versions.java
    }
    kotlinOptions {
        jvmTarget = build_versions.java_build
    }
}

dependencies {
    // Modules
    implementation project(path: ':data')
    implementation project(path: ':domain:impl')
    implementation project(path: ':domain:public')

    // Android support libraries
    implementation deps.support.appcompat
    implementation deps.support.material
    implementation deps.support.core_ktx
    implementation deps.support.activity_ktx
    implementation deps.support.viewmodel_ktx
    implementation deps.support.constraintlayout

    // Coroutines
    implementation deps.kotlin.coroutines_core

    // DI
    implementation deps.di.hilt
    kapt deps.di.hilt_kapt

    testImplementation deps.atsl.junit
    androidTestImplementation deps.atsl.ext_junit
    androidTestImplementation deps.atsl.espresso
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}